
-----------------------------------------------------------
-- print 100 simulated states of conway's game of life
-- for a glider pattern
-- print generation number along with cells

output =
  initCells
  |> iterate(updateCells)
  |> take(100)
  |> enumerate
  |> map(showPair)
  |> concatMap(printFrame)

-- need to concat results from printFrames to join the
-- commands into one big sequence

initCells =
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]
  [0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]

width  = length(initCells[0])
height = length(initCells)

positions = 
  for y in range(height)
  for x in range(width)
  yield (x, y)

-----------------------------------------------------------
-- update each cell in the grid according to its position,
-- and convert the resulting list back to a 2D array

updateCells(cells) =
  positions
  |> map(tick(cells))
  |> toNDArray((height, width))

-----------------------------------------------------------
-- get the new value for a cell at a give position
-- based on the current cell values in the grid

tick(cells, pos) = toInt(survive or birth) where {
  survive = cells[y][x] == 1 and count in {2, 3}
  birth   = cells[y][x] == 0 and count == 3
  count   = getCount(x, y, cells)
  (x, y)  = pos
}

-----------------------------------------------------------
-- get the number of live neighbors of a given position

getCount(x, y, cells) = sum(neighborVals) where {
  neighborVals = 
    for dx in deltas 
    for dy in deltas 
    when (dx != 0 or dy != 0)
    yield getNeighbor(x + dx, y + dy, cells) 
  deltas = [-1, 0, 1]
}

getNeighbor(x, y, cells) = cells[y % height][x % width]   

-----------------------------------------------------------
-- print the board and generation number given pairs
-- of (gen, cells) from the enumerate function

showPair(pair) =
  format("{}\ngeneration: {}", [showCells(cells), gen])
  where (gen, cells) = pair

showCells(cells) =
  cells
  |> map(showRow)
  |> join("\n")

showRow(row) =
  format("|{}|", [map(showCell, row) |> join("")])

showCell(cell) =
  if cell == 1 then "*" else " "
